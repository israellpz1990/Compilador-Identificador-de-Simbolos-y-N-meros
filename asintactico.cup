 
   
/* ---------------Seccion de declaraciones preliminares--------------------*/

package ejemplocup;
import java_cup.runtime.*;
import java.io.FileReader;
   
/* Codigo del parser, se copia integramente a la clase final.
    Agregamos el manejo de errores. */

parser code {: 
    
    /* Reporte de error encontrado. */
    public void report_error(String message, Object info) {
        StringBuilder m = new StringBuilder("Error");
        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
            if (s.left >= 0) {                
                m.append(" in line "+(s.left+1));
                if (s.right >= 0)
                    m.append(", column "+(s.right+1));
            ;
            }
            System.out.println("**********"+s.sym+"************");
            if(s.sym == 2){
               message = "Expresion incompleta, Falta un Entero antes del PUNTOCOMA";
          } else if (s.sym == 0){
        message = "No se coloco el fin de línea";           
        }else if (s.sym == 3){
        message = "Expresion Contiene simbolos seguidos";           
        }
        
        m.append(" : "+message);
        System.err.println(m);
    }
  }

    /* Cuando se encuentra un error de donde el sistema no puede
        recuperarse, se lanza un error fatal. Se despliega el mensaje
        de error y se finaliza la ejecucion. */

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

    /* Metodo main para garantizar la ejecucion del analizador
       lexico y sintactico, ademas que se pase como parametro la tabla
       de simbolos correspondiente. */

    public static void main(String[] args){
        try {
            AnalizadorSintactico asin = new AnalizadorSintactico(
                    new AnalizadorLexico( new FileReader(args[0])));
            Object result = asin.parse().value;
            System.out.println("\n*** Resultados finales ***");
        } catch (Exception ex) {
            ex.printStackTrace();
        }
    }
:};
   

   
/* ------------Declaracion de simbolos terminales y no terminales---------- */
   
/* Terminales (tokens obtenidos por el analizador lexico).  

   Terminales que no tienen un valor son listados primero, los terminales que
   tienen un valor como los enteros son listados en la segunda o demas lineas. */

terminal           PUNTOCOMA, SUMANDO, RESTANDO, MULTIPLICANDO, PARENIZQ, PARENDER;
terminal Integer   ENTERO;
   
/* No terminales usados en la seccion gramatical. 

   Primero se lista los no terminales que tienen un valor Object y despues
   se lista los no terminales que tienen un entero. Un Object se refiere a que
   no tienen tipo, pudiendo ser entero o String. */

non terminal Object     expr_list, expr_part;
non terminal Integer    expr, factor, termino;
   

/* -------------Seccion de predencia y asociacion de los terminales----------- */
   
/*
  Precedencia de los no terminales, no sirve con simbolos terminales.
  Por eso no la usamos. Además indica si se asocia a izquierda o derecha.
*/
  /*precedence left SUMANDO, RESTANDO; 
  precedence left MULTIPLICANDO;*/


/* ------------------- Seccion de la gramatica ------------------------ */
   


   
/* 'expr_list' es la raiz de la gramatica. Una 'expr_list' puede ser una 'expr_list'
   seguida de una expr_part, o puede ser una expr_part.
   Un terminal o no terminal se define <termino> ::= termino1 termino2 ... terminoN.;
   donde termino puede ser terminal o no terminal, solo se permite un simbolo a la
   izquierda. La | sirve para indicar que es una produccion u otra.
   Debemos pasar de simbolos no terminales a simbolos terminales. Una gramatica que no
   termina en simbolos terminales se dice que no reduce, y por lo tanto nunca se finaliza
   su procesado.
*/
   
    expr_list ::= expr_list expr_part
                  |
                  expr_part
                  ;
                  
/* 'expr_part' se compone de una expr seguida de PUNTOCOMA, o de una asign seguida de PUNTOCOMA.
   asign:a esta estableciendo que a representa al objeto asociado a asign, entre {: :}
   se coloca el sodigo en java para las acciones a tomar al cumplirse una produccion.
*/
   
   expr_part ::= expr:e
                  {:
                    if(e==2){
                    System.out.println(" = DOS"); }else if(e==3){
                   System.out.println(" = TRES"); }else if(e==4){
                   System.out.println(" = CUATRO");} else if(e==5){
                   System.out.println(" = CINCO");} else if(e==6){
                   System.out.println(" = SEIS");} else if(e==7){
                   System.out.println(" = SIETE");} else if(e==8){
                   System.out.println(" = OCHO");} else if(e==9){
                   System.out.println(" = NUEVE");} else if(e==10){
                   System.out.println(" = DIEZ");} else if(e==11){
                   System.out.println(" = ONCE");}
                  :} 
                  PUNTOCOMA
                 ;
                  
/* 'expr' puede ser una expresion que inicia por numero entero SUMANDO expr,
  de esa forma se realiza una suma. Tambien puede ser un numero entero, con esto
  se devuelve el valor del entero. En RESULT se almacena el valor de las acciones,
  y se pasa al siguiente nivel de la gramatica.
*/              

   expr      ::=  expr:e SUMANDO factor:f
                  {:
                    RESULT = new Integer(e.intValue() + f.intValue());
                  :}
                  |
                  expr:e RESTANDO factor:f
                  {:
                    RESULT = new Integer(e.intValue() - f.intValue());
                  :}
                  |
                  factor:n
                  {:
                    RESULT = n; 
                  :}
                  ;

factor        ::=  factor:f MULTIPLICANDO termino:e
                  {:
                    RESULT = new Integer(f.intValue() * e.intValue());
                  :}
                  |
                  termino:t
                  {:
                    RESULT = t;
                  :}
                  ;

termino       ::=  PARENIZQ expr:e PARENDER
                  {:
                    RESULT = e;
                  :}
                  |
                  ENTERO:e
                  {:
                    RESULT = e;
                  :}
                  ; 